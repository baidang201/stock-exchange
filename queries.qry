query selectSaleProposalOfCompany {
  description: "select sale proposals of specific company's share"
  statement:
      SELECT org.se.exchange.Proposal
        WHERE (company == _$company AND type == 'FOR_SALE' AND price <= _$price)
        
}

query selectShareByUserAndCompanyLimit {
  description: "select the shares os specific company belonging to specific user"
  statement:
      SELECT org.se.exchange.Share
        WHERE (holder == _$owner AND company == _$company)
        LIMIT _$limit
}

query selectShareByUserAndCompany {
  description: "select the shares os specific company belonging to specific user"
  statement:
      SELECT org.se.exchange.Share
        WHERE (holder == _$owner AND company == _$company)
}

query selectBuyProposalOfCompany {
  description: "select buy proposals of specific company's share"
  statement:
      SELECT org.se.exchange.Proposal
        WHERE (company == _$company AND type == 'FOR_BUY' AND price >= _$price)
        
}
